plugins {
    id "org.springframework.boot" version "3.3.4"
    id "io.spring.dependency-management" version "1.1.6"
    id "com.google.cloud.tools.jib" version "3.4.3"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.diffplug.spotless" version "7.0.2"
    id "java"
}

group = "nl.carosi"
version = "0.6.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register('createVersionFile') {
    doLast {
        def versionFile = file("$buildDir/resources/main/version.properties")
        versionFile.text = "version=${project.version}\n"
    }
}

tasks.named('processResources') {
    dependsOn 'createVersionFile'
}

// Formatting
tasks.compileJava.dependsOn 'spotlessApply'
spotless {
  java {
    importOrder()
    removeUnusedImports()
    cleanthat()
    googleJavaFormat()
    formatAnnotations()
  }
}

jib {
    from {
        image = "eclipse-temurin:17-jre"
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }

    }
    container {
        jvmFlags = ["-Xshare:auto", "-XX:TieredStopAtLevel=1", "-XX:CICompilerCount=1", "-XX:+UseSerialGC", "-Xmx512m"]
    }
    to {
        image = "ghcr.io/nov1n/remarkable-pocket"
        tags = [project.version.toString()]
    }
    extraDirectories {
        paths = 'src/main/jib'
        permissions = [
                '/usr/local/bin/rmapi*': '755'
        ]
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation("com.positiondev.epublib:epublib-core:3.1") {
        exclude group: "org.slf4j"
    }
    implementation "com.fasterxml.jackson.core:jackson-core:2.18.0"
    implementation "com.github.tchudyk:pocket-integration:1.2.2"
    implementation "com.google.guava:guava:33.3.1-jre"
    implementation "info.picocli:picocli:4.7.6"
    implementation "net.lingala.zip4j:zip4j:2.11.5"
    implementation "org.springframework.boot:spring-boot-starter-web:3.3.4"
    implementation "org.springframework.retry:spring-retry:2.0.9"
    implementation "org.springframework:spring-aspects:6.1.13"
    implementation 'org.xerial:sqlite-jdbc:3.46.1.3'
    testImplementation "org.springframework.boot:spring-boot-starter-test:3.3.4"
}

test {
    useJUnitPlatform()
}
